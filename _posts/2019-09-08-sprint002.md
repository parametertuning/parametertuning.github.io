---
title: TopProver - Sprint Round 2
author: cdddar
---

[top-prover.top/contest/3](http://top-prover.top/contest/3)

問題名の後に `(* 後から解いた *)` とあるのは、コンテスト終了後に通した解法です。

## #12: Summand of one

$$\forall m, n \in \mathbb N, m + n = 1 \implies m = 0 \lor n = 0$$

$m$, $n$ を構造について一回ずつ場合分け (case) すると、
どちらかが $0$ の場合が3つと、どちらも $1$ 以上の場合に分けられる。
前者の場合はゴールの後件が満たされるのでok。
後者の場合は $m+n=1$ が満たされないことを言えばいい。
$m + n = 1$ を $2 + \cdot = 1$ の形に変形すれば、あとは `done.` でなんとかなる。

## #13: Definitions of injectivity

2つの定義

- $I_1 : f(m) = f(n) \implies m = n$
- $I_2 : m \ne n \implies f(m) \ne f(n)$

が等しいことを示せという問題。
（これらは関数の単射の定義だけど、実はそんな中身はどうでもよくて）
直感的には一方は他方の対偶になっている。
なので2つが等しいのは当たり前。

対偶が等しいことはもちろん一般的に証明できる。

```coq
(* 対偶は等しい *)
Lemma contrapositive : forall (P Q: Prop), (P -> Q) -> (~Q -> ~P).
Proof.
  unfold not.
  move => P Q PQ notQ.
  move/PQ.
  exact.
Qed.
```

$\ne$ の定義が $=$ の否定なので、$I_2$ が $I_1$ の対偶になっているのは明らか。
ただし $\ne$ の否定が $=$ になるのはすぐには出てこない。
けどライブラリを眺めていると

```coq
coq < Check Nat.eq_dne.
Nat.eq_dne
     : forall n m : nat, ~ n <> m <-> n = m
```

があり、二重否定の消去がすでに定義されているので、これを使えばよい。

## #14: Multiplication in F_2 is idempotent (* 上手くいかなかった *)

`mod2` という関数は自然数の 2 の剰余を取る。
このとき
$$\forall n \in \mathbb N, \mathrm{mod2}(n) = \mathrm{mod2}(n^2)$$
を示せ。

偶奇に着目すれば $n$ のパリティと $n^2$ のパリティは一致する。
そして `mod2` という関数はパリティのみで値が決まる二値関数。

というわけで以下の補題を使えば示せることまでは分かったけど、時間が足りず失敗。
補題がさらなる補題を呼び、という感じだった。。。

```coq
(* mod2 は 0 か 1 しか取らない。特に 2 以上の値は取らない。
  (mod2 n) の値で場合分けするときに必要
*)
Lemma mod2_lt_2 : forall m, 2 > mod2 m.
Proof.
  elim => [//|n H].
  simpl.
  case (mod2 n).
  - done.
  - done.
Qed.

(*
  あと全然時間が足りなくて証明出来なかったけど次の4つの補題は成り立つはず
 *)

Lemma fu_even : forall n, 0 = mod2 n <-> Nat.Even n.
Admitted.

Lemma fu_odd : forall n, 1 = mod2 n <-> Nat.Odd n.
Admitted.

Lemma sq_even : forall n, Nat.Even n -> Nat.Even (n * n).
Admitted.

Lemma sq_odd : forall n, Nat.Odd n -> Nat.Odd (n * n).
Admitted.

(*
以上の材料があれば次のように証明できる
*)

Theorem solution : task.
Proof.
  unfold task.
  move => n.
  remember (mod2 n) as k eqn:E.
  destruct k.
  move: E.
  rewrite !fu_even.
  by apply sq_even.

  destruct k.
  move: E.
  rewrite !fu_odd.
  by apply sq_odd.

  move: (mod2_lt_2 n).
  rewrite -E.
  done.
Qed.
```

まず、自然数に関する場合分けは構造に関してやれば「0 か 1 以上か」になるわけで、
それをもう一度場合分けして「0 か 1 か 2 以上か」にし、 `mod2_lt_2` によって 2 以上を省く、ということをやった。
けど、パリティに関する場合分けをしたほうが証明は簡潔になる予感がした。
そのような場合分けの書き方が分からなかったんだけど。

## #14: Multiplication in F_2 is idempotent (* 後から解いた *)

- `Nat.Even` `Nat.Odd` は ssrnat と相性が悪いので使わない
  - 大したこと無いので自分で定義し直してもいい？
  - 今の場合、初めから `mod2` の値をそのまま使えばいい
- パリティで場合分けする
  - 以下のように

```coq
Lemma parity : forall n, mod2 n = 0 \/ mod2 n = 1.
Admitted
```

今 `mod2 n` の値が 0 か 1 かで場合分けしたいとき、
（つまり `n` が偶数か奇数かで場合分けしたいとき）

```coq
n : nat
=======
P

< move: (parity n).

n : nat
=======
mod2 n = 0 \/ mod2 n = 1 -> P

< case.

2 subgoals

n : nat
=======
mod2 n = 0 -> P

n : nat
=======
mod2 n = 1 -> P
```

となって、実質場合分けが出来る。

これと合わせて結局次を順に示した。
ここで `mod2 n` を $n \bmod 2$ と書く。

1. Lemma 1: $\forall n, n \bmod 2 = 0 \lor n \bmod 2 = 1$
1. Lemma 2: $\forall n, (n + 2) \bmod 2 = n \bmod 2$
1. Lemma 3: $\forall n, k, (n + 2k) \bmod 2 = n \bmod 2$
  - これの証明に Lemma 2 を使う

```coq
(* Lemma 1 *)
Lemma parity : forall n, mod2 n = 0 \/ mod2 n = 1.
Proof.
  elim => [|n].
  - by left.
  - case.
    - move => H.
      right.
      simpl.
      by rewrite H.
    - move => H.
      left.
      simpl.
      by rewrite H.
Qed.

(* Lemma 2 *)
Lemma mod2_add2 : forall n, mod2 (n.+2) = mod2 n.
Proof.
  move => n.
  simpl.
  remember (mod2 n) as k.
  destruct k.
  - done.
  - destruct k.
    - done.
    - move: (parity n).
      rewrite -Heqk.
      case.
      - done.
      - done.
Qed.

(* Lemma 3 *)
Lemma mod2_add_even : forall n k, mod2 (n + 2 * k) = mod2 n.
Proof.
  move => n k.
  elim k.
  - rewrite muln0.
    rewrite addn0.
    done.
  - move => k'.
    move => H.
    rewrite mulnSr.
    rewrite addnA.
    rewrite addn2.
    rewrite mod2_add2.
    exact.
Qed.

Theorem solution: task.
Proof.
  unfold task.
  elim.
  - done.
  - move => n I.
    rewrite mulnSr.
    rewrite mulSnr.
    have: (n * n + n + n.+1 = n * n + 1 + 2 * n). {
      rewrite -addnA.
      rewrite -addnA.
      rewrite mul2n.
      rewrite -addnn.
      rewrite -addn1.
      rewrite [(1 + (n + n))] addnA.
      rewrite [1 + n + n] addnC.
      rewrite [1 + n] addnC.
      done.
    }
    move => RW.
    rewrite RW.
    rewrite mod2_add_even.
    move: (parity n).
    case.
    - (* H: mod2 n = 0 *)
      move => H.
      simpl.
      rewrite H.
      rewrite addn1.
      simpl.
      rewrite -I.
      rewrite H.
      done.
    - (* H: mod2 n = 1 *)
      move => H.
      rewrite addn1.
      simpl.
      rewrite -I.
      rewrite !H.
      done.
Qed.
```

## #15: Identity permutation (* 後から解いた *)

```coq
Coq < Check Permutation.
Permutation
     : forall A : Type, seq A -> seq A -> Prop

Coq < Print Permutation.
Inductive Permutation (A : Type) : seq A -> seq A -> Prop :=
    perm_nil : Permutation [] []
  | perm_skip : forall (x : A) (l l' : seq A),
                Permutation l l' -> Permutation (x :: l) (x :: l')
  | perm_swap : forall (x y : A) (l : seq A),
                Permutation [:: y, x & l] [:: x, y & l]
  | perm_trans : forall l l' l'' : seq A,
                 Permutation l l' -> Permutation l' l'' -> Permutation l l''
```

`Permutation` は2つのリストについて一方が他方のちょうど並び替えになってるかを判定する述語らしい。
このとき

```coq
    Permutation (a :: l) (b :: l) -> a = b.
```

を示せというもの。

[Coq.Sorting.Permutation](https://coq.inria.fr/library/Coq.Sorting.Permutation.html) ライブラリを眺めていると使えそうな定理があって、私は次を使うことにした。

```coq
Theorem Permutation_app_inv_r l l1 l2 :
 Permutation (l1 ++ l) (l2 ++ l) -> Permutation l1 l2.
```

後件が前件を分解した形になっていて、それも頭だけ使っているのがいい。
ただし今はこれをさらに特化した形で、`l1` `l2` がそれぞれ `[a]` `[b]` になっているパターン。
つまり

```coq
solution <       Check (Permutation_app_inv_r l [a] [b]).
Permutation_app_inv_r l [a] [b]
     : Permutation ([a] ++ l)%list ([b] ++ l)%list -> Permutation [a] [b]
```

これが使える。

> ()%list とついているのは、ssreflect を使っているとリストが seq で上書きされるからで、
> 上の定理は Coq の素の list として定義されているので、seq じゃなくて list だと言うために自動的にアノテートされている。
> 基本的に seq も list も同一のものに見えるのだが、あくまでも違う型という扱いなので、定理が適用できなかったりして面倒だった。
> 非本質的だ。

定理の帰結として `Permutation [a] [b]` が得られる。
最終的なゴールは `a=b` なのだが、これは自明に思える。
Permutation ライブラリを見てると

```coq
Lemma Permutation_length_1: forall a b, Permutation [a] [b] -> a = b.
```

という、まんまなものがあったのでこれを使えばいい。

細かな処理として、 `a :: l` を `[a] ++ l` にしなければいけない。
これは計算すれば分かることなので、`done` で解ける。

```coq
Coq < Lemma cons_app : forall (T: Type) (a: T) (l: list T), a :: l = [a] ++ l.
1 subgoal
  
  ============================
  forall (T : Type) (a : T) (l : seq.seq T), (a :: l)%SEQ = [a] ++ l

cons_app <   done.
No more subgoals.

cons_app < Qed.
cons_app is defined
```

以上を使って次のように解けた。

```coq
Definition task :=
    forall a b (l : list nat),
        Permutation (a :: l) (b :: l) -> a = b.

Theorem solution: task.
Proof.
  unfold task.
  intros a b l.
  rewrite [a :: l] cons_app.
  rewrite [b :: l] cons_app.
  move/ (Permutation_app_inv_r l [a] [b]).
  apply: Permutation_length_1.
Qed.
```


