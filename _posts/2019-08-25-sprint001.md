---
title: TopProver - Sprint Round 1
author: cdddar
---

[top-prover.top/contest/2](http://top-prover.top/contest/2)

リアルタイムには参加してないので全て後からゆっくり解いた

## #9: forall l: list nat, l @ [0] <> []

`@` はいわゆる `++` (concat) のこと。
`<>` はもちろん `!=` のことで、実際には `~ ( _ = _ )` で定義されている。
したがって `unfold not` すれば `(_ = _) -> False` という形になって、これで証明しやすくなることもあるかもしれない、ないかも。

`l@` とかいう余計なのがあるけどとにかく左辺は `[0]` をさらに伸ばしたもので、右辺は空のリスト。
だから明らかに長さが違うことに注目できる。
左辺は `l` がどんなでも `[0]` がある以上は長さが 1 以上ある。
そして右辺は空なんだから長さはゼロだ。
それに注目してやればいい。

`Coq.List` ライブラリを眺めると、それに使えそうな2つの補題がある。
一つは `List.length_zero_iff_nil` で、長さがゼロなのは空リスト (`nil`) であることと同値である主張。
$$\mathrm{length}~l = 0 \iff l = []$$

2つ目は、2つのリストを結合した長さは、各リストの長さを足したものに等しいという主張 `app_length`。
$$\mathrm{length}~(l_1 + l_2) = (\mathrm{length}~l_1) + (\mathrm{length}~l_2)$$

実際には `l @` の部分はどうでもよくて、`[0] <> []` なんだけど、まあ、ゴールにそれがあるのでしょうがない。


```coq
(* おまじない *)
From mathcomp Require Import all_ssreflect.
Require Import List.

(* 次を証明する *)
Theorem solution: forall l, app l (0 :: nil) <> nil.
Proof.
```

```coq
1 subgoal

  ============================
  forall l : seq.seq nat, l ++ [:: 0] <> [::]

solution < move => l.
1 subgoal

  l : seq.seq nat
  ============================
  l ++ [:: 0] <> [::]

solution < rewrite -List.length_zero_iff_nil.
(* 頭のマイナスは補題を逆向きに使うことを意味する
   補題は、長さがゼロならば空、なので、
   その逆の、空ならば長さがゼロを適用した
*)

1 subgoal

  l : seq.seq nat
  ============================
  length (l ++ [:: 0])%list <> 0

solution < rewrite List.app_length.
(* 長さとリストの結合は分配できる *)
1 subgoal

  l : seq.seq nat
  ============================
  (length l + length [:: 0])%coq_nat <> 0

solution < simpl.
(* 関数定義から従う簡約だけを行う
  ここでは具体的な値である [0] を length に適用した
  計算結果は 1 だと分かるので 1 で置き換えた
*)
1 subgoal
  
  l : seq.seq nat
  ============================
  (length l + 1)%coq_nat <> 0

(*
  +1 という計算を .+1 に置き換える
  .+1 はペアノ公理で言う successor のこと
 *)
solution < rewrite [(_ + 1)%coq_nat] addn1.
1 subgoal
  
  l : seq.seq nat
  ============================
  (length l).+1 <> 0
```

最後はいわゆる後続関数（サクセサー）を適用した値が 0 でないことを言っている。
ペアノの公理ではこれは公理に入っている。
つまり `+1` して `0` になるような値はない
$$\forall x, x + 1 \ne 0$$
ということを言っている。

それを主張してくれる公理を探すと、

```coq
solution < Search (_.+1 <> 0).
Warning: Listing only lemmas with conclusion matching (_.+1 <> 0)
PeanoNat.Nat.neq_succ_0: forall n : nat, n.+1 <> 0
```

こうして見つかるので、

```coq
solution < apply PeanoNat.Nat.neq_succ_0.
No more subgoals.

solution < Qed.
move => l.
rewrite -List.length_zero_iff_nil.
rewrite List.app_length.
(simpl).
rewrite [(_ + 1)%coq_nat]addn1.
(apply PeanoNat.Nat.neq_succ_0).

Qed.
solution is defined
```

として証明が終了した。

## #10: Cumulative sum of list

2つのリストに関する帰納法を回せという問題。

五十嵐淳先生の講義資料
[計算と論理 その5](https://www.fos.kuis.kyoto-u.ac.jp/~igarashi/class/cal13w/handout5.pdfhttps://www.fos.kuis.kyoto-u.ac.jp/~igarashi/class/cal13w/handout5.pdf)
の p40 あたりにもある通り、2つのモノに関する帰納法を回す場合は全称量化子について最新の注意が必要。
上手くやらないと失敗する。

- とりあえず帰納法で分解するもの以外は全称量化子を付けてゴールに入れておく
- 時々、今表示されてるコンテキストとゴールで本当に証明できるか人手でチェックする

```
(* From mathcomp Require Import ssreflect ssrfun eqtype *)
From mathcomp Require Import all_ssreflect.
Require Import Nat List Arith Omega ZArith.
Import ListNotations.
Set Implicit Arguments.
Unset Strict Implicit.
Unset Printing Implicit Defensive.


Fixpoint prefix_sum sum l :=
  match l with
  | nil => sum :: nil
  | head :: tail => sum :: prefix_sum (sum + head) tail
  end.

Fixpoint plus_list l1 l2 :=
  match (l1, l2) with
  | (nil, _) => nil
  | (_, nil) => nil
  | (h1 :: t1, h2 :: t2) => (h1 + h2) :: plus_list t1 t2
  end.

Definition task :=
  forall l1 l2,
    prefix_sum 0 (plus_list l1 l2) =
    plus_list (prefix_sum 0 l1) (prefix_sum 0 l2).

Definition task' :=
  forall x1 x2 l1 l2,
    prefix_sum (x1 + x2) (plus_list l1 l2) =
    plus_list (prefix_sum x1 l1) (prefix_sum x2 l2).

Theorem solution': task'.
Proof.
  unfold task'.
  move => x1 x2 l1.
  revert x1 x2.
  (* 今コンテキストは l1 のみで、 x1 x2 l2 はゴールに forall で付いてる *)
  induction l1.
  - (* base case; when l1 = [] *)
    move => x1 x2 l2.
    case l2.
    - (* when l2 = [] *)
      by simpl.
    - (* when l2 = n :: l *)
      move => n l.
      by simpl.
  - (* induction case;
       Goal (forall x1 x2 l2, task'(x1, x2, l1, l2)) -> (forall x1 x2 l2, task'(x1, x2, (a::l) l2))
    *)
    move => x1 x2 l2.
    case l2.
    - (* when l2 = [] *)
      simpl.
      assert (plus_list_l_nil : forall l, plus_list l nil = nil). {
        (* これは自明だけど simpl では2つ目の match に行く前に l の match で詰まるので計算が進まない *)
        (* l について帰納法を回す *)
        induction l.
        - by simpl.
        - by simpl.
      }
      by rewrite plus_list_l_nil.
    - (* when l2 = n :: l *)
      move => n l.
      (* いま、帰納法の仮定に forall x1 x2 l2, task'(x1, x2, l1 l2) があって
         ゴールが task'(x1, x2, (a :: l1) (n :: l)).
      *)
      simpl.
      (* 引数の cons の分だけ計算が進む
         今ゴールは
          (x1 + x2 :: prefix_sum (x1 + x2 + (a + n)) (plus_list l1 l))%SEQ =
          (x1 + x2 :: plus_list (prefix_sum (x1 + a) l1) (prefix_sum (x2 + n) l))%SEQ
      *)
      assert (addnA4 : (x1 + x2 + (a + n)) = (x1 + a) + (x2 + n)). {
        (* 仮定を使うために形を揃えておく *)
        by omega.
      }
      rewrite addnA4.
      rewrite IHl1. (* 帰納法の仮定を用いた *)
      done.
Qed.
```
